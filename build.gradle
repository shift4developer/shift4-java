plugins {
    id 'maven-publish'
    id 'java'
    id 'signing'
}

group = "com.shift4"
version = "1.2.0"
archivesBaseName = "shift4-java"

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileTestJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

test {
    useJUnitPlatform()
    reports {
        junitXml.required = false
        html.required = true
    }
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.apache.httpcomponents:httpmime:4.5.13'

    testImplementation 'io.github.cdimascio:dotenv-java:2.2.4'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'commons-io:commons-io:2.11.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

ext.genResourcesDir = file("$buildDir/generated-resources")
task generateBuildProperties {
    ext.buildPropertiesFile = file("$genResourcesDir/com/shift4/build.properties")
    outputs.file(buildPropertiesFile)
    doLast {
        buildPropertiesFile.text = """\
                |shift4java.build.version=$project.version
                |shift4java.build.timestamp=${new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")}
        """.stripMargin().trim()
    }
}
sourceSets.main.output.dir genResourcesDir, builtBy: generateBuildProperties

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "com.shift4"
            artifactId = 'shift4-java'
            from components.java
            pom {
                name = 'Shift4 Java Library'
                description = 'Java client library for Shift4 API'
                url = 'https://dev.shift4.com'
                licenses {
                    license {
                        name = 'MIT license'
                        url = 'https://github.com/shift4developer/shift4-java/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        name = 'Shift4'
                        email = 'devsupport@shift4.com'
                    }
                }
                scm {
                    url = 'https://github.com/shift4developer/shift4-java.git'
                    connection = 'scm:git:git@github.com:shift4developer/shift4-java.git'
                    developerConnection = 'scm:git:git@github.com:shift4developer/shift4-java.git'
                }
            }
        }
    }
    repositories {
        maven {
            url = findProperty("nexusUrl") ?: "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = findProperty("nexusUsername")
                password = findProperty("nexusPassword")
            }
        }
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

if (findProperty("signingKey") != null) {
    signing {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}

repositories {
    mavenCentral()
}